dnl Process this file with autoconf to produce a `configure' script.
dnl To rebuild `configure' from this, execute the command
dnl 	autoconf
dnl in the directory containing this script.
AC_PREREQ(2.9)dnl
AC_INIT(psgml.el)

PACKAGE=psgml
VERSION=1.0
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

AC_PREFIX_PROGRAM(etags)

EMACS=emacs
AC_MSG_CHECKING(xemacs)
AC_ARG_WITH(xemacs,
[  --with-xemacs=XEMACS     Build the package with and for XEmacs
                            (XEMACS=no).],
[     case "${withval}" in
        e | em | ema| emac | emacs ) val=emacs ;;
        x | xe | xem | xema| xemac | xemacs ) val=xemacs ;;
        y | ye | yes )      val=xemacs  ;;
        n | no )        val=emacs  ;;
        * ) val=xemacs
        ;;
      esac
      EMACS=$val
])
AC_MSG_RESULT($EMACS)

dnl will override --with-xemacs; is this acceptable?
AC_MSG_CHECKING(emacs)
AC_ARG_WITH(emacs,
[  --with-emacs=EMACS    EMACS=emacs or EMACS=xemacs],
[     case "${withval}" in
        e | em | ema| emac | emacs ) val=emacs ;;
        g | gn | gnu ) val=emacs ;;
        x | xe | xem | xema| xemac | xemacs ) val=xemacs ;;
        y | ye | yes )      val=emacs  ;;
        n | no )        val=xemacs  ;;
        * )         val=emacs
        ;;
      esac
      EMACS=$val
])
AC_MSG_RESULT($EMACS)

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_INSTALL

dnl Check where to install .elc files.
ke_PATH_LISPDIR

AC_SUBST(EMACS)
AC_OUTPUT(Makefile)

dnl configure.in ends here
